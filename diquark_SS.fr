M$ModelName = "diquark_SS";

M$Information = {
   Authors -> {"Flynn Linton"},
   Institutions -> {"ETH Zurich"},
   Emails -> {"flinton@student.ethz.ch"},
   Version -> {"1.0"}
   };

(****************************)
(*****   GROUP THEORY   *****)
(****************************)

(*** GROUP THEORY INDICES ***)
IndexRange[Index[Sextet]] = NoUnfold[Range[6]]; (* SU(3) sextet *)
IndexStyle[Sextet, u];
AddGaugeRepresentation[SU3C -> {T6, Sextet}];


(****************************)
(****   DIQUARK FIELDS   ****)
(****************************)

M$ClassesDescription = {
  
  (*** PHYSICAL FIELDS ***)

  S[23] == {  (* NOTE: DiqSS -> SU3 6, DiqSSbar -> SU3 6bar*)
    ClassName -> DiqSS,
    SelfConjugate -> False,
    Indices -> {Index[Sextet]},
    QuantumNumbers -> {(*Y -> 1/3,*) Q -> 1/3},
    Mass -> {MdqSS, 1},
    Width -> {WdqSS, auto},
    PropagatorLabel -> "DiqSS+1/3",
    PropagatorArrow -> True,
    PropagatorType -> Dashes,
    ParticleName -> "DiqSS",
    FullName -> "DiqSS",
    PDG -> 9990006
    }
  }

(****************************)
(******   PARAMETERS   ******)
(****************************)

M$Parameters = {

  (*** DIQUARK COUPLINGS ***)

  QLQLss == {
		BlockName -> DiqQLQLss,
    Indices -> {Index[Generation], Index[Generation]},
    Definitions -> 
     QLQLss[i_, j_] :> 0 /; NumericQ[i] && NumericQ[j] && (i != j),
    Value -> {
      QLQLss[1, 1] -> 0.1,
      QLQLss[2, 2] -> 0.1,
      QLQLss[3, 3] -> 0.1
      },
    InteractionOrder -> {QCD, 1},
    ParameterType -> External
    },

  uRdRss == {
		BlockName -> DiquRdRss,
    Indices -> {Index[Generation], Index[Generation]},
    Definitions -> 
     uRdRss[i_, j_] :> 0 /; NumericQ[i] && NumericQ[j] && (i != j),
    Value -> {
      uRdRss[1, 1] -> 0.1,
      uRdRss[2, 2] -> 0.1,
      uRdRss[3, 3] -> 0.1
      },
    InteractionOrder -> {QCD, 1},
    ParameterType -> External
    }
  
  }

(****************************)
(******   Lagrangian   ******)
(****************************)

(*** DIQUARK KINETIC ***)

(* (D_mu Phi)* (D^mu Phi) - m^2 Phi *)
LdqKinSS = DC[DiqSSbar[r], mu]*DC[DiqSS[r], mu] - 
           MdqSS^2*(DiqSSbar[r]*DiqSS[r]);

(*** DIQUARK INT ***)

(* QbarL^c QL Phi_SS *)
LdqSSa1 = Eps[a, c]*QLQLss[c, b]*(CC[QLbar][sp, m, a, i]*QL[sp, n, b, j]*Eps[m, n])*(K6[r, i, j]*DiqSSbar[r]);
LdqSSa = LdqSSa1 + HC[LdqSSa1];

(* ubarR^c dR Phi_SS *)
LdqSSb1 = uRdRss[a, b]*(ProjP[m, n]*CC[uqbar][m, a, i]*dq[n, b, j])*(K6[r, i, j]*DiqSSbar[r]);
LdqSSb = LdqSSb1 + HC[LdqSSb1];

LdqIntSS = LdqSSa + LdqSSb;
LdqSS = LdqKinSS + LdqIntSS;
