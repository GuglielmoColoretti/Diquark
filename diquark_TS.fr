M$ModelName = "diquark_TS";

M$Information = {
   Authors -> {"Flynn Linton"},
   Institutions -> {"ETH Zurich"},
   Emails -> {"flinton@student.ethz.ch"},
   Version -> {"1.0"}
   };

(****************************)
(****   DIQUARK FIELDS   ****)
(****************************)

M$ClassesDescription = {
  
  (*** PHYSICAL FIELDS ***)

  S[24] == {
    ClassName -> DiqTS,
    SelfConjugate -> False,
    Indices -> {Index[Colour]},
    QuantumNumbers -> {(*Y -> -1/3,*) Q -> -1/3},
    Mass -> {MdqTS, 1},
    Width -> {WdqTS, auto},
    PropagatorLabel -> "DiqTS-1/3",
    PropagatorArrow -> True,
    PropagatorType -> Dashes,
    ParticleName -> "DiqTS",
    FullName -> "DiqTS",
    PDG -> 9990007
    }
  }

(****************************)
(******   PARAMETERS   ******)
(****************************)

M$Parameters = {

  (*** DIQUARK COUPLINGS ***)

  QLQLts == {
		BlockName -> DIQ,
    Indices -> {Index[Generation], Index[Generation]},
    Definitions -> 
     QLQLts[i_, j_] :> 0 /; NumericQ[i] && NumericQ[j] && (i != j),
    Value -> {
      QLQLts[1, 1] -> 0.1,
      QLQLts[2, 2] -> 0.1,
      QLQLts[3, 3] -> 0.1
      },
    InteractionOrder -> {QCD, 1},
    ParameterType -> External
    },

    uRdRts == {
 		BlockName -> DiquRdRts,
    Indices -> {Index[Generation], Index[Generation]},
    Definitions -> 
     uRdRts[i_, j_] :> 0 /; NumericQ[i] && NumericQ[j] && (i != j),
    Value -> {
      uRdRts[1, 1] -> 0.1,
      uRdRts[2, 2] -> 0.1,
      uRdRts[3, 3] -> 0.1
      },
    InteractionOrder -> {QCD, 1},
    ParameterType -> External
    }
  }

(****************************)
(******   Lagrangian   ******)
(****************************)

(*** DIQUARK KINETIC ***)

(* (D_mu Phi)* (D^mu Phi) - m^2 Phi *)
LdqKinTS = DC[DiqTSbar[r], mu]*DC[DiqTS[r], mu] - 
          MdqTS^2*(DiqTSbar[r]*DiqTS[r]);

(*** DIQUARK INT ***)

(* QbarL^c QL Phi_TS *)
LdqTSa1 = QLQLts[a, b]*(CC[QLbar][sp, m, a, i]*QL[sp, n, b, j]*Eps[m, n])*(K3[r, i, j]*DiqTS[r]);
LdqTSa = LdqTSa1 + HC[LdqTSa1];

(* ubarR^c dR Phi_TS *)
LdqTSb1 = uRdRts[a, b]*(ProjP[m, n]*CC[uqbar][m, a, i]*dq[n, b, j])*(K3[r, i, j]*DiqTS[r]);
LdqTSb = LdqTSb1 + HC[LdqTSb1];

LdqIntTS = LdqTSa + LdqTSb;
LdqTS = LdqKinTS + LdqIntTS;
